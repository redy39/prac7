  Warning: FormalParameters is deletable
  Warning: Arguments is deletable

   1 using Library;
   2 
   3 COMPILER Parva $NC
   4 /* Parva level 1 grammar  - Coco/R for C#
   5    P.D. Terry, Rhodes University, 2015
   6    Pascal/Modula operator precedences with short circuit evaluation
   7    Void functions, value parameter passing
   8    No globals or value returning functions
   9    int and bool types supported
  10    Bradshaw -- version for Practical 7, 2019  */
  11 
  12   public static bool // tied to pragmas/directives
  13     debug    = false,
  14     listCode = false,
  15     warnings = true;
  16   static int progState = 0;
  17   const bool
  18     known = true;
  19 
  20   // This next method might better be located in the code generator.  Traditionally
  21   // it has been left in the ATG file, but that might change in future years
  22   //
  23   // Not that while sequences like \n \r and \t result in special mappings to lf, cr and tab,
  24   // other sequences like \x \: and \9 simply map to x, ; and 9 .  Most students don't seem
  25   // to know this!
  26 
  27   static string Unescape(string s) {
  28   /* Replaces escape sequences in s by their Unicode values */
  29     StringBuilder buf = new StringBuilder();
  30     int i = 0;
  31     while (i < s.Length) {
  32       if (s[i] == '\\') {
  33         switch (s[i+1]) {
  34           case '\\': buf.Append('\\'); break;
  35           case '\'': buf.Append('\''); break;
  36           case '\"': buf.Append('\"'); break;
  37           case  'r': buf.Append('\r'); break;
  38           case  'n': buf.Append('\n'); break;
  39           case  't': buf.Append('\t'); break;
  40           case  'b': buf.Append('\b'); break;
  41           case  'f': buf.Append('\f'); break;
  42           default:   buf.Append(s[i+1]); break;
  43         }
  44         i += 2;
  45       }
  46       else {
  47         buf.Append(s[i]);
  48         i++;
  49       }
  50     }
  51     return buf.ToString();
  52   } // Unescape
  53 
  54   // the following is global for expediency (fewer parameters needed)
  55 
  56   static Label mainEntryPoint = new Label(!known);
  57 
  58   static bool IsArith(int type) {
  59     return type == Types.intType || type == Types.noType;
  60   } // IsArith
  61 
  62   static bool IsBool(int type) {
  63     return type == Types.boolType || type == Types.noType;
  64   } // IsBool
  65 
  66   static bool IsArray(int type) {
  67     return (type % 2) == 1;
  68   } // IsArray
  69 
  70   static bool Compatible(int typeOne, int typeTwo) {
  71   // Returns true if typeOne is compatible (and comparable for equality) with typeTwo
  72     return    typeOne == typeTwo
  73            || IsArith(typeOne) && IsArith(typeTwo)
  74            || typeOne == Types.noType
  75            || typeTwo == Types.noType
  76            || IsArray(typeOne) && typeTwo == Types.nullType
  77            || IsArray(typeTwo) && typeOne == Types.nullType;
  78   } // Compatible
  79 
  80   static bool Assignable(int typeOne, int typeTwo) {
  81   // Returns true if a variable of typeOne may be assigned a value of typeTwo
  82     return    typeOne == typeTwo
  83            || typeOne == Types.noType
  84            || typeTwo == Types.noType
  85            || IsArray(typeOne) && typeTwo == Types.nullType;
  86   } // Assignable
  87 
  88   static bool IsCall(out DesType des) {
  89   // Used as an LL(1) conflict resolver variable/function name
  90     Entry entry = Table.Find(la.val);
  91     des = new DesType(entry);
  92     return entry.kind == Kinds.Fun;
  93   } // IsCall
  94 
  95 
  96 /* -------------------------------------------------------------------------- */
  97 
  98 CHARACTERS
  99   lf         = CHR(10) .
 100   backslash  = CHR(92) .
 101   control    = CHR(0) .. CHR(31) .
 102   letter     = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz" .
 103   digit      = "0123456789" .
 104   stringCh   = ANY - '"' - control - backslash .
 105   charCh     = ANY - "'" - control - backslash .
 106   printable  = ANY - control .
 107 
 108 TOKENS
 109   identifier = letter { { "_" } (letter | digit) } .
 110   number     = digit { digit } .
 111   stringLit  = '"' { stringCh | backslash printable } '"' .
 112   charLit    = "'" ( charCh   | backslash printable ) "'" .
 113 
 114 PRAGMAS
 115   DebugOn     = "$D+" .                      (. debug = true; .)
 116   DebugOff    = "$D-" .                      (. debug = false; .)
 117   StackDump   = "$SD" .                      (. CodeGen.Stack(); .)
 118   HeapDump    = "$HD" .                      (. CodeGen.Heap(); .)
 119   TableDump   = "$ST" .                      (. Table.PrintTable(OutFile.StdOut); .)
 120 
 121 COMMENTS FROM "//" TO lf
 122 COMMENTS FROM "/*" TO "*/"
 123 
 124 IGNORE CHR(9) .. CHR(13)
 125 
 126 PRODUCTIONS
 127 
 128   Parva
 129                                              (. CodeGen.FrameHeader();
 130                                                 // no arguments
 131                                                 CodeGen.Call(mainEntryPoint);
 132                                                 // forward, incomplete
 133                                                 CodeGen.LeaveProgram(); .)     // return to O/S
 134   = {   FuncDeclaration } EOF                (. if(!mainEntryPoint.IsDefined())
 135                                                   SemError("missing Main function"); progState = 1; .)
 136   .
 137 
 138   FuncDeclaration      
 139                        (. StackFrame frame = new StackFrame();
 140                           Entry function = new Entry(); .)
 141   =  "void" Ident<out function.name>  
 142                                              (. function.kind = Kinds.Fun;
 143                                                 function.type = Types.voidType;
 144                                                 function.nParams = 0;
 145                                                 function.firstParam = null;
 146                                                 function.entryPoint = new Label(known);
 147                                                 Table.Insert(function);
 148                                                 Table.OpenScope(); .)
 149      "(" FormalParameters<function> ")"
 150                                              (. frame.size = CodeGen.headerSize + 
 151                                                 function.nParams;
 152                                                 if (function.name.ToUpper().Equals("MAIN")
 153                                                 && !mainEntryPoint.IsDefined()
 154                                                 && function.nParams == 0) {
 155                                                   mainEntryPoint.Here(); } .)
 156      Body<frame>                             (. Table.CloseScope(); .)
 157   .
 158 
 159   FormalParameters<Entry func>               (. Entry param; .)
 160   = [ OneParam<out param, func>              (. func.firstParam = param; .)
 161       { WEAK "," OneParam<out param, func> }
 162     ] .
 163 
 164   OneParam<out Entry param, Entry func>
 165                                              (. param = new Entry();
 166                                                 param.kind = Kinds.Var;
 167                                                 param.offset = CodeGen.headerSize + func.nParams;
 168                                                 func.nParams++; .)
 169   =  Type<out param.type>
 170      Ident<out param.name>                   (. Table.Insert(param); .)
 171   .
 172 
 173   Body<StackFrame frame>                     (. Label DSPLabel = new Label(known);
 174                                                 int sizeMark = frame.size;
 175                                                 CodeGen.OpenStackFrame(0); .)
 176   =  "{" { Statement<frame> }
 177      WEAK "}"                                (. CodeGen.FixDSP(DSPLabel.Address(), frame.size - sizeMark);
 178                                                 CodeGen.LeaveVoidFunction(); .)
 179 .
 180 
 181   Statement<StackFrame frame>
 182   =  SYNC (   Block<frame>
 183             | ConstDeclarations
 184             | VarDeclarations<frame>
 185             | AssignmentOrCall
 186             | IfStatement<frame>
 187             | WhileStatement<frame>
 188             | HaltStatement
 189             | ReturnStatement
 190             | ReadStatement
 191             | WriteStatement
 192             | ";"
 193           ) .
 194 
 195   Block<StackFrame frame>
 196   =                                          (. Table.OpenScope(); .)
 197      "{" { Statement<frame> }
 198      WEAK "}"                                (. Table.CloseScope(); .)
 199   .
 200 
 201   ConstDeclarations
 202   = "const" OneConst { WEAK "," OneConst }
 203     WEAK ";"
 204   .
 205 
 206   OneConst                                   (. Entry constant = new Entry();
 207                                                 ConstRec con; .)
 208   =  Ident<out constant.name>                (. constant.kind = Kinds.Con; .)
 209      AssignOp Constant<out con>              (. constant.value = con.value;
 210                                                 constant.type = con.type;
 211                                                 Table.Insert(constant); .)
 212   .
 213 
 214   Constant<out ConstRec con>                 (. con = new ConstRec(); .)
 215   =   IntConst<out con.value>                (. con.type = Types.intType; .)
 216     | CharConst<out con.value>               (. con.type = Types.intType; .)
 217     | "true"                                 (. con.type = Types.boolType; con.value = 1; .)
 218     | "false"                                (. con.type = Types.boolType; con.value = 0; .)
 219     | "null"                                 (. con.type = Types.nullType; con.value = 0; .)
 220   .
 221 
 222   VarDeclarations<StackFrame frame>          (. int type; .)
 223   =  Type<out type>                          
 224      VarList<frame, type>
 225      WEAK ";"
 226   .
 227 
 228   Type<out int type>                         
 229   =   BasicType<out type>
 230        [ "[]"                                (. type++; .)
 231        ] .
 232 
 233   BasicType<out int type>                    (. type = Types.noType; .)
 234   =   "int"                                  (. type = Types.intType; .)
 235     | "bool"                                 (. type = Types.boolType; .)
 236   .
 237 
 238 
 239   VarList<StackFrame frame, int type>
 240   =  OneVar<frame, type>
 241      { WEAK "," OneVar<frame, type> }
 242   .
 243 
 244   OneVar<StackFrame frame, int type>         (. int expType;
 245                                                 Entry var = new Entry(); .)
 246   =  Ident<out var.name>                     (. var.kind = Kinds.Var;
 247                                                 var.type = type;
 248                                                 var.offset = frame.size;
 249                                                 frame.size++; .)
 250      [ AssignOp                              (. CodeGen.LoadAddress(var); .)
 251        Expression<out expType>               (. if (!Assignable(var.type, expType))
 252                                                   SemError("incompatible types in assignment");
 253                                                 progState = 1;
 254                                                 CodeGen.Assign(var.type); .)
 255      ]                                       (. Table.Insert(var); .)
 256   .
 257 
 258 
 259   AssignmentOrCall                           (. int expType;
 260                                                 DesType des; .)
 261   =  (  IF (IsCall(out des))                    // /* use resolver to handle LL(1) conflict */
 262          identifier                          (. CodeGen.FrameHeader(); .)
 263          "(" Arguments<des> ")"              (. CodeGen.Call(des.entry.entryPoint); .)
 264       |  Designator<out des>                 (. if (des.entry.kind != Kinds.Var)
 265                                                   SemError("cannot assign to " + Kinds.kindNames[des.entry.kind]); 
 266                                                 progState = 1; .)
 267          AssignOp
 268          Expression<out expType>             (. if (!Assignable(des.type, expType))
 269                                                   SemError("incompatible types in assignment");
 270                                                 progState = 1;
 271                                                 CodeGen.Assign(des.type); .)
 272      ) WEAK ";"
 273   .
 274 
 275   Arguments<DesType des>                     (. int argCount = 0;
 276                                                 Entry fp = des.entry.firstParam; .)
 277   =  [ OneArg<fp>                            (. argCount++; if (fp != null) fp = fp.nextInScope; .)
 278        { WEAK "," OneArg<fp>                 (. argCount++; if (fp != null) fp = fp.nextInScope;.)
 279        }
 280      ]                                       (. if (argCount != des.entry.nParams)
 281                                                   SemError("wrong number of arguments"); 
 282                                                 progState = 1; .)
 283   .
 284 
 285   OneArg<Entry fp>                           (. int argType; .)
 286   =  (  Expression<out argType>              (. if (fp != null && !Assignable(fp.type, argType))
 287                                                   SemError("argument type mismatch"); 
 288                                                 progState = 1; .)
 289      )
 290   .
 291 
 292   Designator<out DesType des>                (. string name;
 293                                                 int indexType; .)
 294   =  Ident<out name>                         (. Entry entry = Table.Find(name);
 295                                                 if (!entry.declared)
 296                                                   SemError("undeclared identifier");
 297                                                 progState = 1;
 298                                                 des = new DesType(entry);
 299                                                 if (entry.kind == Kinds.Var)
 300                                                   CodeGen.LoadAddress(entry); .)
 301      [     "["                               (. if (IsArray(des.type)) des.type--;
 302                                                 else { SemError("unexpected subscript");
 303                                                   progState = 1; }
 304                                                 if (des.entry.kind != Kinds.Var) {
 305                                                   SemError("unexpected subscript");
 306                                                   progState = 1; }
 307                                                 CodeGen.Dereference(); .)
 308               Expression<out indexType>      (. if (!IsArith(indexType)) {
 309                                                   SemError("invalid subscript type");
 310                                                 progState = 1; }
 311                                                 CodeGen.Index(); .)
 312 
 313            "]"
 314      ]
 315   .
 316 
 317   IfStatement<StackFrame frame>
 318                                              (. Label falseLabel = new Label(!known); .)
 319   =  "if" "(" Condition ")"                  (. CodeGen.BranchFalse(falseLabel); .)
 320         Statement<frame>                     (. falseLabel.Here(); .)
 321   .
 322 
 323 
 324   WhileStatement<StackFrame frame>
 325                                              (. Label loopExit  = new Label(!known);
 326                                                 Label loopStart = new Label(known); .)
 327   =  "while" "(" Condition ")"               (. CodeGen.BranchFalse(loopExit); .)
 328      Statement<frame>              	         (. CodeGen.Branch(loopStart);
 329                                                 loopExit.Here(); .)
 330   .
 331 
 332   HaltStatement
 333   =  "halt"
 334      [
 335      "(" 
 336      [ WriteList ]
 337      ")"
 338      ]                                       (. CodeGen.LeaveProgram(); .)
 339      WEAK ";"
 340   .
 341 
 342   ReturnStatement              
 343   =  "return"                                (. CodeGen.LeaveVoidFunction(); .)
 344     WEAK ";"
 345   .
 346 
 347   ReadStatement
 348   = (   "read"     "("   ReadList   ")"
 349       | "readLine" "(" [ ReadList ] ")"      (. CodeGen.ReadLine(); .)
 350     )
 351     WEAK ";"
 352   .
 353 
 354   ReadList
 355   = ReadElement { WEAK "," ReadElement }
 356   .
 357 
 358   ReadElement                                (. string str;
 359                                                 DesType des; .)
 360   =   StringConst<out str>                   (. CodeGen.WriteString(str); .)
 361     | Designator<out des>                    (. if (des.entry.kind != Kinds.Var) {
 362                                                   SemError("wrong kind of identifier");
 363                                                   progState = 1; }
 364                                                 switch (des.type) {
 365                                                   case Types.intType:
 366                                                   case Types.boolType:
 367                                                     CodeGen.Read(des.type); break;
 368                                                   default:
 369                                                     SemError("cannot read this type"); progState = 1; break;
 370                                                 } .)
 371   .
 372 
 373   WriteStatement
 374   = (   "write"     "("   WriteList   ")"
 375       | "writeLine" "(" [ WriteList ] ")"    (. CodeGen.WriteLine(); .)
 376     )
 377     WEAK ";"
 378   .
 379 
 380   WriteList
 381   = WriteElement { WEAK "," WriteElement }
 382   .
 383 
 384   WriteElement                               (. int expType;
 385                                                 string str; .)
 386   =   StringConst<out str>                   (. CodeGen.WriteString(str); .)
 387     | Expression<out expType>                (. if (!(IsArith(expType) || expType == Types.boolType)) {
 388                                                   SemError("cannot write this type"); progState = 1; }
 389                                                 switch (expType) {
 390                                                   case Types.intType:
 391                                                   case Types.boolType:
 392                                                     CodeGen.Write(expType); break;
 393                                                   default:
 394                                                     break;
 395                                                 } .)
 396   .
 397 
 398   Condition                                  (. int type; .)
 399   =  Expression<out type>                    (. if (!IsBool(type)) {
 400                                                   SemError("Boolean expression needed"); progState = 1; } .)
 401   .
 402 
 403   Expression<out int type>                   (. int type2;
 404                                                 int op;
 405                                                 bool comparable; .)
 406   =  AddExp<out type>
 407      [ RelOp<out op> AddExp<out type2>       (. switch (op) {
 408                                                   case CodeGen.ceq: case CodeGen.cne:
 409                                                     comparable = Compatible(type, type2);
 410                                                     break;
 411                                                   default:
 412                                                     comparable = IsArith(type) && IsArith(type2);
 413                                                     break;
 414                                                 }
 415                                                 if (!comparable) {
 416                                                   SemError("incomparable operands");
 417                                                   progState = 1; }
 418                                                 type = Types.boolType; CodeGen.Comparison(op); .)
 419      ]
 420   .
 421 
 422   AddExp<out int type>                       (. int type2;
 423                                                 int op;
 424                                                 Label shortcircuit = new Label(!known); .)
 425   =                                          (. type = Types.noType; .)
 426      (   "+" Term<out type>                  (. if (!IsArith(type)) {
 427                                                   SemError("arithmetic operand needed"); 
 428                                                   progState = 1; } .)
 429        | "-" Term<out type>                  (. if (!IsArith(type)) {
 430                                                   SemError("arithmetic operand needed");
 431                                                   progState = 1; }
 432                                                 CodeGen.NegateInteger(); .)
 433        | Term<out type>
 434      )
 435      { AddOp<out op>                         (. if (op == CodeGen.or)
 436                                                   CodeGen.BooleanOp(shortcircuit, CodeGen.or); .)
 437        Term<out type2>                       (. switch (op) {
 438                                                   case CodeGen.or:
 439                                                     if (!IsBool(type) || !IsBool(type2)) {
 440                                                       SemError("boolean operands needed"); 
 441                                                       progState = 1; }
 442                                                     type = Types.boolType;
 443                                                     break;
 444                                                   default:
 445                                                     if (!IsArith(type) || !IsArith(type2)) {
 446                                                       SemError("arithmetic operands needed");
 447                                                       type = Types.noType;
 448                                                       progState = 1;
 449                                                     }
 450                                                     CodeGen.BinaryOp(op);
 451                                                     break;
 452                                                 } .)
 453      }                                       (. shortcircuit.Here(); .)
 454   .
 455 
 456   Term<out int type>                         (. int type2;
 457                                                 int op;
 458                                                 Label shortcircuit = new Label(!known); .)
 459   =  Factor<out type>
 460      { MulOp<out op>                         (. if (op == CodeGen.and)
 461                                                   CodeGen.BooleanOp(shortcircuit, CodeGen.and); .)
 462        Factor<out type2>                     (. switch (op) {
 463                                                   case CodeGen.and:
 464                                                     if (!IsBool(type) || !IsBool(type2)) {
 465                                                       SemError("boolean operands needed"); progState = 1; }
 466                                                     type = Types.boolType;
 467                                                     break;
 468                                                   default:
 469                                                     if (!IsArith(type) || !IsArith(type2)) {
 470                                                       SemError("arithmetic operands needed");
 471                                                       type = Types.noType;
 472                                                       progState = 1;
 473                                                     }
 474                                                     CodeGen.BinaryOp(op);
 475                                                     break;
 476                                                 } .)
 477      }                                       (. shortcircuit.Here(); .)
 478   .
 479 
 480   Factor<out int type>                       (. type = Types.noType;
 481                                                 int size;
 482                                                 DesType des;
 483                                                 ConstRec con; .)
 484   =    Designator<out des>                   (. type = des.type;
 485                                                 switch (des.entry.kind) {
 486                                                   case Kinds.Var:
 487                                                     CodeGen.Dereference();
 488                                                     break;
 489                                                   case Kinds.Con:
 490                                                     CodeGen.LoadConstant(des.entry.value);
 491                                                     break;
 492                                                   default:
 493                                                     SemError("wrong kind of identifier");
 494                                                     progState = 1;
 495                                                     break;
 496                                                 } .)
 497                                         
 498      | Constant<out con>                     (. type = con.type;
 499                                                 CodeGen.LoadConstant(con.value); .)
 500      | "new" BasicType<out type>             (. type++; .)
 501          "[" Expression<out size>            (. if (!IsArith(size)){
 502                                                   SemError("array size must be integer");
 503                                                   progState = 1; }
 504                                                 CodeGen.Allocate(); .)
 505          "]"
 506      | "!" Factor<out type>                  (. if (!IsBool(type)) { SemError("boolean operand needed"); progState = 1; } 
 507                                                 else CodeGen.NegateBoolean();
 508                                                 type = Types.boolType; .)
 509      | "(" Expression<out type> ")"
 510   .
 511 
 512   AddOp<out int op>                          (. op = CodeGen.nop; .)
 513   =    "+"                                   (. op = CodeGen.add; .)
 514      | "-"                                   (. op = CodeGen.sub; .)
 515      | "||"                                  (. op = CodeGen.or;  .)
 516   .
 517 
 518   MulOp<out int op>                          (. op = CodeGen.nop; .)
 519   =    "*"                                   (. op = CodeGen.mul; .)
 520      | "/"                                   (. op = CodeGen.div; .)
 521      | "%"                                   (. op = CodeGen.rem; .)
 522      | "&&"                                  (. op = CodeGen.and; .)
 523   .
 524 
 525   RelOp<out int op>                          (. op = CodeGen.nop; .)
 526   =    "=="                                  (. op = CodeGen.ceq; .)
 527      | "!="                                  (. op = CodeGen.cne; .)
 528      | "<"                                   (. op = CodeGen.clt; .)
 529      | "<="                                  (. op = CodeGen.cle; .)
 530      | ">"                                   (. op = CodeGen.cgt; .)
 531      | ">="                                  (. op = CodeGen.cge; .)
 532   .
 533 
 534   AssignOp = "=" .
 535 
 536   Ident<out string name>
 537   =  identifier                              (. name = token.val; .)
 538   .
 539 
 540   StringConst<out string str>
 541   =  stringLit                               (. str = token.val;
 542                                                 str = Unescape(str.Substring(1, str.Length - 2)); .)
 543   .
 544 
 545   CharConst<out int value>
 546   =  charLit                                 (. string str = token.val;
 547                                                 str = Unescape(str.Substring(1, str.Length - 2));
 548                                                 value = str[0]; .)
 549   .
 550 
 551   IntConst<out int value>
 552   =  number                                  (. try {
 553                                                   value = Convert.ToInt32(token.val);
 554                                                 } catch (Exception) {
 555                                                   value = 0; SemError("number out of range"); progState = 1;
 556                                                 } .)
 557   .
 558 
 559 END Parva.
 560 

0 errors detected
2 warnings detected
